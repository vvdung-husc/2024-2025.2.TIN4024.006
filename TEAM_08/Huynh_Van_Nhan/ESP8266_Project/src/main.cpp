#define BLYNK_TEMPLATE_ID "TMPL6RQGDLOQe" 
#define BLYNK_TEMPLATE_NAME "ESP8266" 
#define BLYNK_AUTH_TOKEN "HkHpsRJ8SZ-wbZcBynAWmAqATjJOvHQV" 

#include <Arduino.h>
#include "utils.h"

#include <ESP8266WiFi.h>
#include <BlynkSimpleEsp8266.h>

#include <Wire.h>
#include <U8g2lib.h>

#include <WiFiClientSecure.h>
#include <UniversalTelegramBot.h>
#include <ArduinoJson.h>

// ThÃ´ng tin WiFi
const char* ssid = "CNTT-MMT";    
const char* password = "13572468";

// Token bot Telegram
const char* botToken = "8027625067:AAHdREtzh1lGcXPLv2VNkIr8-Lo5ffGE3pI";

// Group ID Telegram
const String chatId = "-1002439452600";

// ChÃ¢n LED Ä‘iá»u khiá»ƒn Ä‘Ã¨n giao thÃ´ng
#define gPIN 15
#define yPIN 2
#define rPIN 5

#define OLED_SDA 13
#define OLED_SCL 12

// Khai bÃ¡o Ä‘á»‘i tÆ°á»£ng OLED
U8G2_SH1106_128X64_NONAME_F_HW_I2C oled(U8G2_R0, /* reset=*/ U8X8_PIN_NONE);

bool blinkMode = false;
bool trafficActive = true;  // Biáº¿n Ä‘á»ƒ kiá»ƒm soÃ¡t tráº¡ng thÃ¡i Ä‘Ã¨n

unsigned long upTime = 0;
float fTemperature = 0.0;
float fHumidity = 0.0;

BlynkTimer timer;

// Khai bÃ¡o Ä‘á»‘i tÆ°á»£ng Telegram
WiFiClientSecure client;
UniversalTelegramBot bot(botToken, client);
unsigned long lastTimeBotRan;
const int botRequestDelay = 1000;  // Kiá»ƒm tra tin nháº¯n má»—i giÃ¢y

// Nháº­n lá»‡nh tá»« Blynk Ä‘á»ƒ báº­t cháº¿ Ä‘á»™ nháº¥p nhÃ¡y Ä‘Ã¨n vÃ ng
BLYNK_WRITE(V3) {
  blinkMode = param.asInt();
}

// Hiá»ƒn thá»‹ trÃªn OLED
void updateOLED() {
  oled.clearBuffer();
  oled.setFont(u8g2_font_unifont_t_vietnamese2);

  oled.drawUTF8(0, 14, ("Nhiet do: " + String(fTemperature, 1) + "Â°C").c_str());
  oled.drawUTF8(0, 28, ("Do am: " + String(fHumidity, 1) + "%").c_str());
  oled.drawUTF8(0, 42, ("Uptime: " + String(upTime) + "s").c_str());

  oled.sendBuffer();
}

// Äiá»u khiá»ƒn Ä‘Ã¨n giao thÃ´ng
void TrafficLightControl() {
  static unsigned long lastTimer = 0;
  static int state = 0;
  static const unsigned long durations[] = {2000, 3000, 1000}; 
  static const int ledPins[] = {rPIN, gPIN, yPIN};

  if (!trafficActive) {  // Náº¿u Ä‘Ã¨n bá»‹ táº¯t tá»« Telegram
    digitalWrite(rPIN, LOW);
    digitalWrite(yPIN, LOW);
    digitalWrite(gPIN, LOW);
    return;
  }

  if (blinkMode) {  // Cháº¿ Ä‘á»™ nháº¥p nhÃ¡y Ä‘Ã¨n vÃ ng
    if (millis() - lastTimer > 500) {
      lastTimer = millis();
      digitalWrite(yPIN, !digitalRead(yPIN));
    }
    digitalWrite(rPIN, LOW);
    digitalWrite(gPIN, LOW);
    return;
  }

  // Cháº¿ Ä‘á»™ hoáº¡t Ä‘á»™ng bÃ¬nh thÆ°á»ng
  if (millis() - lastTimer > durations[state]) {
    lastTimer = millis();
    digitalWrite(ledPins[state], LOW);
    state = (state + 1) % 3;
    digitalWrite(ledPins[state], HIGH);
  }
}

// Gá»­i dá»¯ liá»‡u lÃªn Blynk
void sendToBlynk() {
  Blynk.virtualWrite(V0, upTime);      
  Blynk.virtualWrite(V1, fTemperature);
  Blynk.virtualWrite(V2, fHumidity);
}

// Sinh dá»¯ liá»‡u nhiá»‡t Ä‘á»™ & Ä‘á»™ áº©m ngáº«u nhiÃªn
float randomTemperature() {
  return random(-400, 800) / 10.0;
}

float randomHumidity() {
  return random(0, 1000) / 10.0;
}

// Cáº­p nháº­t nhiá»‡t Ä‘á»™ & Ä‘á»™ áº©m
void updateSensorData() {
  static unsigned long lastTimer = 0;
  if (millis() - lastTimer < 2000) return;
  lastTimer = millis();

  fTemperature = randomTemperature();
  fHumidity = randomHumidity();
}

// Cáº­p nháº­t thá»i gian cháº¡y
void updateupTime() {
  static unsigned long lastTimer = 0;
  if (millis() - lastTimer < 1000) return;
  lastTimer = millis();
  upTime++;
}

// Xá»­ lÃ½ tin nháº¯n tá»« Telegram
void handleNewMessages(int numNewMessages) {
  for (int i = 0; i < numNewMessages; i++) {
    String chat_id = bot.messages[i].chat_id;
    String text = bot.messages[i].text;
    Serial.println("Tin nháº¯n nháº­n Ä‘Æ°á»£c: " + text);

    if (chat_id == chatId) {  // Kiá»ƒm tra Ä‘Ãºng group ID
      if (text == "/traffic_off") {
        trafficActive = false;
        digitalWrite(rPIN, LOW);
        digitalWrite(yPIN, LOW);
        digitalWrite(gPIN, LOW);
        bot.sendMessage(chatId, "ðŸš¦ ÄÃ¨n giao thÃ´ng Ä‘Ã£ táº¯t.", "");
      } else if (text == "/traffic_on") {
        trafficActive = true;
        bot.sendMessage(chatId, "ðŸš¦ ÄÃ¨n giao thÃ´ng hoáº¡t Ä‘á»™ng trá»Ÿ láº¡i.", "");
      }
    }
  }
}

// ThÃªm biáº¿n thá»i gian cho cáº£nh bÃ¡o Telegram
unsigned long lastAlertTime = 0;
const unsigned long alertInterval = 60 * 1000;  // 1 phÃºt

// Gá»­i cáº£nh bÃ¡o náº¿u nhiá»‡t Ä‘á»™ hoáº·c Ä‘á»™ áº©m nguy hiá»ƒm
void checkAndSendAlert() {
  unsigned long currentTime = millis();
  if (currentTime - lastAlertTime < alertInterval) return; // Chá»‰ gá»­i má»—i 5 phÃºt

  String alertMessage = "ðŸš¨ *Cáº¢NH BÃO Sá»¨C KHá»ŽE!* ðŸš¨\n";
  bool shouldSendAlert = false;

  if (fTemperature < 10) {
    alertMessage += "ðŸŒ¡ï¸ *Nhiá»‡t Ä‘á»™ quÃ¡ tháº¥p!* Nguy cÆ¡ háº¡ thÃ¢n nhiá»‡t, tÃª cÃ³ng.\n";
    shouldSendAlert = true;
  } else if (fTemperature >= 30 && fTemperature <= 35) {
    alertMessage += "ðŸ”¥ *Nhiá»‡t Ä‘á»™ cao!* CÃ³ thá»ƒ gÃ¢y máº¥t nÆ°á»›c, má»‡t má»i.\n";
    shouldSendAlert = true;
  } else if (fTemperature > 35 && fTemperature <= 40) {
    alertMessage += "âš ï¸ *Cáº£nh bÃ¡o nÃ³ng!* Nguy cÆ¡ sá»‘c nhiá»‡t, chuá»™t rÃºt, say náº¯ng.\n";
    shouldSendAlert = true;
  } else if (fTemperature > 40) {
    alertMessage += "ðŸš‘ *Cá»±c ká»³ nguy hiá»ƒm!* CÃ³ thá»ƒ gÃ¢y suy ná»™i táº¡ng, Ä‘á»™t quá»µ nhiá»‡t.\n";
    shouldSendAlert = true;
  }

  if (fHumidity < 30) {
    alertMessage += "ðŸ’¨ *Äá»™ áº©m tháº¥p!* CÃ³ thá»ƒ gÃ¢y khÃ´ da, kÃ­ch á»©ng máº¯t.\n";
    shouldSendAlert = true;
  } else if (fHumidity > 70 && fHumidity <= 80) {
    alertMessage += "ðŸŒ«ï¸ *Äá»™ áº©m cao!* TÄƒng nguy cÆ¡ vi khuáº©n vÃ  náº¥m má»‘c.\n";
    shouldSendAlert = true;
  } else if (fHumidity > 80) {
    alertMessage += "ðŸš¨ *Äá»™ áº©m quÃ¡ cao!* CÆ¡ thá»ƒ khÃ³ thoÃ¡t má»“ hÃ´i, dá»… sá»‘c nhiá»‡t.\n";
    shouldSendAlert = true;
  }

  if (shouldSendAlert) {
    bot.sendMessage(chatId, alertMessage, "Markdown");
    lastAlertTime = currentTime; // Cáº­p nháº­t thá»i gian gá»­i cáº£nh bÃ¡o
  }
}



// SETUP
void setup() {
  Serial.begin(115200);

  // Cáº¥u hÃ¬nh LED
  pinMode(gPIN, OUTPUT);
  pinMode(yPIN, OUTPUT);
  pinMode(rPIN, OUTPUT);
  digitalWrite(gPIN, LOW);
  digitalWrite(yPIN, LOW);
  digitalWrite(rPIN, HIGH);

  // Cáº¥u hÃ¬nh OLED
  Wire.begin(OLED_SDA, OLED_SCL);
  oled.begin();
  oled.clearBuffer();
  oled.setFont(u8g2_font_unifont_t_vietnamese1);
  oled.drawUTF8(0, 14, "TrÆ°á»ng ÄHKH");
  oled.drawUTF8(0, 28, "Khoa CNTT");
  oled.drawUTF8(0, 42, "Láº­p trÃ¬nh IoT");
  oled.sendBuffer();

  // Káº¿t ná»‘i WiFi & Blynk
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("\nWiFi connected!");

  Blynk.begin(BLYNK_AUTH_TOKEN, ssid, password);

  client.setInsecure();  // Bá» qua SSL
  bot.sendMessage(chatId, "ðŸš¦ Bot Ä‘iá»u khiá»ƒn Ä‘Ã¨n giao thÃ´ng Ä‘Ã£ khá»Ÿi Ä‘á»™ng!", "");

  // Gá»­i dá»¯ liá»‡u lÃªn Blynk má»—i 3 giÃ¢y
  timer.setInterval(3000L, sendToBlynk);
}

// LOOP
void loop() {
  Blynk.run();
  timer.run();

  TrafficLightControl();
  updateSensorData();
  updateupTime();
  updateOLED();

  // Xá»­ lÃ½ tin nháº¯n Telegram
  if (millis() - lastTimeBotRan > botRequestDelay) {
    int numNewMessages = bot.getUpdates(bot.last_message_received + 1);
    while (numNewMessages) {
      handleNewMessages(numNewMessages);
      numNewMessages = bot.getUpdates(bot.last_message_received + 1);
    }
    lastTimeBotRan = millis();
  }

  // Kiá»ƒm tra vÃ  gá»­i cáº£nh bÃ¡o náº¿u cáº§n
  checkAndSendAlert();
}
