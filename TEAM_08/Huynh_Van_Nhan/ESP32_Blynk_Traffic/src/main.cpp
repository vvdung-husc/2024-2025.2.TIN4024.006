//Hu·ª≥nh VƒÉn Nh√¢n
#define BLYNK_TEMPLATE_ID "TMPL6PqjG-L40"
#define BLYNK_TEMPLATE_NAME "Nhan"
#define BLYNK_AUTH_TOKEN "IOg3DFoDvjBPSbxdbu3OOYGlKXGeGv52"


//Nguy·ªÖn Kh√°nh Ph∆∞·ª£ng
//#define BLYNK_TEMPLATE_ID "TMPL6Ovhz1RbQ"
//#define BLYNK_TEMPLATE_NAME "ESP32 Traffic Light Blynk"
//#define BLYNK_AUTH_TOKEN "LDnnRXGZFlpczlq4J5GHdgLHwA86hqK0"


#define WIFI_SSID "Wokwi-GUEST"
#define WIFI_PASS ""

// Th∆∞ vi·ªán c·∫ßn thi·∫øt
#include <WiFi.h>
#include <BlynkSimpleEsp32.h>
#include <DHT.h>
#include <TM1637Display.h>

#define DHTPIN 16
#define DHTTYPE DHT22
#define GREEN_LED 25
#define YELLOW_LED 26
#define RED_LED 27
#define BLUE_LED 21
#define LDR_PIN 34
#define BUTTON_PIN 23
#define CLK 18
#define DIO 19

DHT dht(DHTPIN, DHTTYPE);
TM1637Display display(CLK, DIO);
BlynkTimer timer;

bool ledState = false;
bool displayState = false;
unsigned long lastButtonPress = 0;
float temperature = 0;
float humidity = 0;
bool useBlynkData = false;

int counter = 0;
int lightState = 0;
unsigned long lightTimer = 0;
int countdown = 5;
bool useBlynkLightData = false;

int darkThreshold = 1000;
bool lowLightMode = false;

float lastTemperature = -1;
float lastHumidity = -1;
int lastLightLevel = -1;
int lastLightState = -1;

// K·∫øt n·ªëi WiFi
void connectWiFi() {
    Serial.print("ƒêang k·∫øt n·ªëi WiFi...");
    WiFi.begin(WIFI_SSID, WIFI_PASS);
    while (WiFi.status() != WL_CONNECTED) {
        delay(500);
        Serial.print(".");
    }
    Serial.println(" ƒê√£ k·∫øt n·ªëi!");
}

// G·ª≠i d·ªØ li·ªáu c·∫£m bi·∫øn l√™n Blynk
void sendSensorData() {
    if (!useBlynkData) {
        float newTemp = dht.readTemperature();
        float newHumidity = dht.readHumidity();
        if (!isnan(newTemp) && !isnan(newHumidity)) {
            temperature = newTemp;
            humidity = newHumidity;
        }
    }

    if (isnan(temperature) || isnan(humidity)) {
        Serial.println("L·ªói ƒë·ªçc c·∫£m bi·∫øn!");
        return;
    }

    
    Blynk.virtualWrite(V0, temperature);
    Blynk.virtualWrite(V1, humidity);
    Blynk.virtualWrite(V3, counter);
    counter++;

    if (temperature != lastTemperature || humidity != lastHumidity) {
        Serial.print("Nhi·ªát ƒë·ªô: ");
        Serial.print(temperature);
        Serial.print(" ¬∞C, ƒê·ªô ·∫©m: ");
        Serial.print(humidity);
        Serial.println(" %");
        lastTemperature = temperature;
        lastHumidity = humidity;
    }
}

// Nh·∫≠n gi√° tr·ªã ng∆∞·ª°ng √°nh s√°ng t·ª´ Blynk
BLYNK_WRITE(V6) {
    darkThreshold = param.asInt();
    useBlynkLightData = true;
    Serial.print("C·∫≠p nh·∫≠t ng∆∞·ª°ng √°nh s√°ng t·ª´ Blynk: ");
    Serial.println(darkThreshold);
}

// Nh·∫≠n nhi·ªát ƒë·ªô t·ª´ Blynk
BLYNK_WRITE(V4) {
    temperature = param.asFloat();
    useBlynkData = true;
}

// Nh·∫≠n ƒë·ªô ·∫©m t·ª´ Blynk
BLYNK_WRITE(V5) {
    humidity = param.asFloat();
    useBlynkData = true;
}

// Ki·ªÉm tra n√∫t nh·∫•n v√† c·∫≠p nh·∫≠t tr·∫°ng th√°i m√†n h√¨nh
void checkButton() {
    if (digitalRead(BUTTON_PIN) == LOW) {
        if (millis() - lastButtonPress > 300) {
            ledState = !ledState;
            digitalWrite(BLUE_LED, ledState);
            Blynk.virtualWrite(V2, ledState);
            displayState = !displayState;
            if (!displayState) {
                display.clear();
            } else {
                display.showNumberDec(countdown);
            }

            lastButtonPress = millis();
        }
    }
}

// ƒêi·ªÅu khi·ªÉn LED t·ª´ Blynk
BLYNK_WRITE(V2) {
    ledState = param.asInt();
    digitalWrite(BLUE_LED, ledState);

    displayState = ledState;

    if (!displayState) {
        display.clear();
    } else {
        display.showNumberDec(countdown);
    }
}

// Qu·∫£n l√Ω √°nh s√°ng th·∫•p

void checkLightLevel() {
    int lightLevel = analogRead(LDR_PIN);
    if (useBlynkLightData) {
        // ∆Øu ti√™n s·ª≠ d·ª•ng ng∆∞·ª°ng t·ª´ Blynk ƒë·ªÉ quy·∫øt ƒë·ªãnh lowLightMode
        lowLightMode = (darkThreshold < 1000);
    } else {
        // N·∫øu ch∆∞a c√≥ d·ªØ li·ªáu t·ª´ Blynk, t·ª± ƒë·ªông l·∫•y t·ª´ c·∫£m bi·∫øn
        darkThreshold = lightLevel;
        lowLightMode = (darkThreshold < 1000);
    }

    if (lightLevel != lastLightLevel) {
        Serial.print("√Ånh s√°ng hi·ªán t·∫°i: ");
        Serial.println(lightLevel);
        lastLightLevel = lightLevel;
    }
}

void updateTrafficLights() {
    checkLightLevel();

    if (lowLightMode) {
        // Ch·∫ø ƒë·ªô √°nh s√°ng y·∫øu: ƒê√®n v√†ng nh·∫•p nh√°y, m√†n h√¨nh hi·ªÉn th·ªã "0000"
        digitalWrite(GREEN_LED, LOW);
        digitalWrite(RED_LED, LOW);
        for (int i = 0; i < 5; i++) {
            digitalWrite(YELLOW_LED, HIGH);
            delay(500);
            digitalWrite(YELLOW_LED, LOW);
            delay(500);
        }

        if (displayState) {
            display.showNumberDec(0000);
        }
    } else {
        // Ch·∫ø ƒë·ªô giao th√¥ng b√¨nh th∆∞·ªùng
        if (millis() - lightTimer >= 1000) {
            lightTimer = millis();

            if (countdown > 0) {
                countdown--;
            } else {
                lightState = (lightState + 1) % 3;
                countdown = (lightState == 0) ? 5 : (lightState == 1) ? 2 : 3;
            }
        }

        digitalWrite(GREEN_LED, lightState == 0);
        digitalWrite(YELLOW_LED, lightState == 1);
        digitalWrite(RED_LED, lightState == 2);

        // C·∫≠p nh·∫≠t m√†n h√¨nh LED n·∫øu ƒëang b·∫≠t
        if (displayState) {
            display.showNumberDec(countdown);
        }

        if (lightState != lastLightState) {
            Serial.println(lightState == 0 ? "üü¢ Xanh" : lightState == 1 ? "üü° V√†ng" : "üî¥ ƒê·ªè");
            lastLightState = lightState;
        }
    }
}

// C·∫•u h√¨nh h·ªá th·ªëng
void setup() {
    Serial.begin(115200);
    connectWiFi();
    Blynk.begin(BLYNK_AUTH_TOKEN, WIFI_SSID, WIFI_PASS);

    pinMode(GREEN_LED, OUTPUT);
    pinMode(YELLOW_LED, OUTPUT);
    pinMode(RED_LED, OUTPUT);
    pinMode(BLUE_LED, OUTPUT);
    pinMode(BUTTON_PIN, INPUT_PULLUP);
    pinMode(LDR_PIN, INPUT);
    display.clear();
    Blynk.virtualWrite(V3, counter);
    Blynk.virtualWrite(V6, darkThreshold);
    dht.begin();
    display.setBrightness(7);
    timer.setInterval(2000L, sendSensorData);
}

void loop() {
    Blynk.run();
    timer.run();
    checkButton();
    updateTrafficLights();
}
